apply plugin: "maven-publish"

publishing {
  publications {
    maven(MavenPublication) {
      artifact "${project.ext.artifact}"

      groupId "${project.ext.groupId}"
      artifactId "${project.ext.artifactId}"
      version "${project.versionName}"

      pom.withXml {
        def dependenciesNode = asNode().appendNode("dependencies")

        project.configurations.implementation.allDependencies.each {
          def depClass = it.class.toString()
          if(!depClass.contains("DefaultExternalModuleDependency")
          && !depClass.contains("DefaultProjectDependency")) {
            println "ignore dependency " + depClass
            return;
          }
          if(it.name.contains("kotlin-stdlib")) {
            println "ignore dependency " + it
            return;
          }
          println "add depend project " + it.name

          def depGroup = it.group
          def depName = it.name
          def depVersion = it.version
          if(depClass.contains("DefaultProjectDependency")) {
            def depProject = it.dependencyProject
            depGroup = depProject.ext.groupId
            depName = depProject.name
            depVersion = depProject.versionName
          }

          println "add dependency " + it
          def dependencyNode = dependenciesNode.appendNode("dependency")
          dependencyNode.appendNode("groupId", depGroup)
          dependencyNode.appendNode("artifactId", depName)
          dependencyNode.appendNode("version", depVersion)
          dependencyNode.appendNode("scope", "compile")
        }
      }
    }
  }
  repositories {
    maven {
      url "${project.buildDir}/repo"
    }
  }
}

tasks.withType(PublishToMavenRepository) {
  it.dependsOn "${project.ext.dependsOn}"
}
